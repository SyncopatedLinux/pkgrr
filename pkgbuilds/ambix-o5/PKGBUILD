# Maintainer: Robert Pannick <rwpannick@gmail.com>
# Contributor: David Runge <dvzrv@archlinux.org>

_pkgname=ambix
pkgname=ambix-o5
pkgver=0.3.0
pkgrel=1
pkgdesc="5th Order Ambix VST, LV2 Plug-ins"
arch=('x86_64' 'x86_64_v3')
url="https://github.com/kronihias/ambix"
license=('GPL2')
provides=('ambix-o5')
conflicts=('ambix')
groups=('lv2-plugins' 'pro-audio' 'vst-plugins')
depends=('gcc-libs' 'glibc' 'libglvnd' 'libsoxr' 'libx11' 'libxext')
makedepends=('alsa-lib' 'cmake' 'eigen' 'fftw' 'freetype2' 'freeglut' 'gendesk' 'glu' 'libxcursor' 'libxinerama' 'jack' 'liblo' 'zita-convolver' 'vst2sdk' 'vst3sdk' 'git')
checkdepends=('lv2lint')
optdepends=('jack: for standalone applications'
            'lv2-host: for LV2 plugins'
            'vst-host: for VST plugins')
source=("git+https://github.com/kronihias/ambix.git")

sha256sums=('SKIP')

_plugin_names=('ambix_binaural' 'ambix_converter' 'ambix_decoder'
'ambix_directional_loudness' 'ambix_encoder_i2' 'ambix_encoder_i4'
'ambix_encoder_i6' 'ambix_encoder_i8' 'ambix_encoder' 'ambix_maxre'
'ambix_mirror' 'ambix_rotator' 'ambix_rotator_z' 'ambix_vmic' 'ambix_warp'
'ambix_widening')

_standalone_names=('ambix_binaural' 'ambix_converter' 'ambix_decoder'
'ambix_directional_loudness' 'ambix_encoder' 'ambix_mirror' 'ambix_rotator'
'ambix_vmic')

prepare() {
  cd "${_pkgname}-$pkgver"

  # setting proper LV2 URIs:
  # https://github.com/kronihias/ambix/issues/27
  # patch -Np1 -i "../${_pkgname}-0.2.10-lv2_plugin_uri.patch"
  # fix for gcc >= 9.1.0
  # https://github.com/kronihias/ambix/issues/26
  # patch -Np1 -i "../${_pkgname}-0.2.10-gcc9.1.0.patch"

  declare -A _descriptions=(
    ['ambix_binaural']="Listen to Ambisonics with Headphones"
    ['ambix_converter']="Convert between Ambisonics Formats"
    ['ambix_decoder']="Playback Ambisonics with Loudspeakers"
    ['ambix_directional_loudness']="Amplify, Attenuate or Filter Out Certain Parts of the Spherical Soundfield"
    ['ambix_encoder']="Encode Audio for Ambisonics"
    ['ambix_mirror']="Adjust Symmetric Components in Ambisonics Streams"
    ['ambix_rotator']="Rotate Sounds for Ambisonics"
    ['ambix_vmic']="Virtual Microphone: Select part of the Soundfield"
  )
  declare -A _generic=(
    ['ambix_binaural']="Binaural Decoder"
    ['ambix_converter']="Ambisonics Formats Converter"
    ['ambix_decoder']="Single-band Decoded Ambisonics"
    ['ambix_directional_loudness']="Spherical Soundfield Amplifier"
    ['ambix_encoder']="Ambisonics Panning"
    ['ambix_mirror']="Soundfield Mirroring"
    ['ambix_rotator']="Ambisonics Sounds Rotator"
    ['ambix_vmic']="Ambisonics Virtual Microphone"
  )

  #TODO: fix desktop file names
  for _standalone in ${_standalone_names[@]}; do
    gendesk -f -n \
            --pkgname "${_standalone}_o5" \
            --name "${_standalone}_o5" \
            --pkgdesc "${_description[${_standalone}]}_o5" \
            --genericname "${_generic[${_standalone}]}" \
            --icon "ambix" \
            --categories "AudioVideo;Audio,Ambisonics"
  done

  git submodule init
  git submodule update
}

build() {
  cd "${_pkgname}-$pkgver"
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DVST2SDKPATH='/usr/include/vst36' \
        -DCMAKE_BUILD_TYPE='Release' \
        -DBUILD_LV2=ON \
        -DBUILD_VST=ON \
        -DBUILD_STANDALONE=ON \
        -DAMBI_ORDER=5 \
        -DWITH_ZITA_CONVOLVER=ON \
        -W no-dev \
        -B build \
        -S .

  make VERBOSE=1 -C build

  (
    cd lv2-ttl-generator
    make
  )

  (
    cd build/_bin
    ../../lv2-ttl-generator/generate-ttl.sh
  )

  (
    cd build/_bin
    for _plugin in ${_plugin_names[@]}; do
      sed -i 's/\<${_plugin}\>/${_plugin}_o5/' "lv2/${_plugin}_o5.lv2/${_plugin}_o5.ttl"
      sed -i 's/\<${_plugin}\>/${_plugin}_o5/' "lv2/${_plugin}_o5.lv2/manifest.ttl"
    done
  )
}

check() {
  cd "${_pkgname}-$pkgver"
  for _plugin in ${_plugin_names[@]}; do
    lv2lint -I "build/_bin/lv2/${_plugin}_o5.lv2/" \
      "https://github.com/kronihias/ambix/${_plugin}" \
      || echo "Known to fail: https://github.com/kronihias/ambix/issues/29"
  done
}

package() {
  depends+=('libasound.so' 'libfftw3f.so' 'libfftw3f_threads.so'
  'libfreetype.so' 'libzita-convolver.so')

  cd "${_pkgname}-$pkgver"

  # install standalone applications
  for _standalone in ${_standalone_names[@]}; do
    install -vDm 755 "build/_bin/standalone/${_standalone}_standalone_o5" \
      "${pkgdir}/usr/bin/${_standalone}_o5"
  done

  # install vst2 plugins
  for _plugin in ${_plugin_names[@]}; do
    install -vDm 755 "build/_bin/vst/${_plugin}_o5.so" \
      "${pkgdir}/usr/lib/vst/${_plugin}_o5.so"
  done

  # install lv2 plugins
  for _plugin in ${_plugin_names[@]}; do
    install -vDm 755 "build/_bin/lv2/${_plugin}_o5.lv2/"*.so \
      -t "${pkgdir}/usr/lib/lv2/${_plugin}_o5.lv2/"
    install -vDm 644 "build/_bin/lv2/${_plugin}_o5.lv2/"*.ttl \
      -t "${pkgdir}/usr/lib/lv2/${_plugin}_o5.lv2/"
  done

  # XDG desktop integration
  install -vDm 644 *".desktop" \
    -t "${pkgdir}/usr/share/applications/"
}
